apply plugin: 'com.android.application'

ext.versionMajor = 4
ext.versionMinor = 0
ext.versionPatch = 0
ext.minimumSdkVersion = 14

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "de.c3nav.droid"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        buildConfigField "String", "WEB_URL", "\"https://35c3.c3nav.de\""
    }
    signingConfigs {
        release {
            storeFile file("../../c3nav.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "c3nav"
            keyPassword System.getenv("KSTOREPWD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            zipAlignEnabled true
        }
    }
    lintOptions {
        disable 'AddJavascriptInterface', 'SetJavaScriptEnabled'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
}
